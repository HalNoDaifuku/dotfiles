# shellcheck disable=SC2148

export ZSHRC_DIR=$HOME

# source command override
function source {
    ensure_zcompiled $1
    builtin source $1
}
function ensure_zcompiled {
    local compiled="$1.zwc"
    if [[ ! -r "$compiled" || "$1" -nt "$compiled" ]]; then
        echo "Compiling $1"
        zcompile $1
    fi
}
ensure_zcompiled ~/.zshrc

# PATH
export PATH="$HOME/.local/bin:$PATH"

# sheldon
export SHELDON_CONFIG_DIR="$HOME/.config/sheldon"
# sheldon cache
cache_dir=${XDG_CACHE_HOME:-$HOME/.cache}
sheldon_cache="$cache_dir/sheldon.zsh"
sheldon_toml="$SHELDON_CONFIG_DIR/plugins.toml"
if [[ ! -r "$sheldon_cache" || "$sheldon_toml" -nt "$sheldon_cache" ]]; then
    mkdir -p $cache_dir
    sheldon source > $sheldon_cache
fi
source "$sheldon_cache"
unset cache_dir sheldon_cache sheldon_toml

source $ZSHRC_DIR/.config/zsh/nonlazy.zsh
zsh-defer source $ZSHRC_DIR/.config/zsh/lazy.zsh

## Delete the same PATH
typeset -U PATH
typeset -U FPATH

zsh-defer unfunction source
